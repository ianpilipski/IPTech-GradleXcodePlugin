plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.11.0'
    id 'maven-publish'
}

repositories {
    jcenter()
}

sourceCompatibility = 1.8

//allow publish key/secret override from environment variable
task setupPluginUpload {
    doLast {
        String key = System.env.gradlePublishKey
        String secret = System.env.gradlePublishSecret
        if(!key || !secret) {
            throw new RuntimeException("gradlePublishKey and/or gradlePublishSecret are not defined environment variables")
        }

        System.properties.setProperty('gradle.publish.key', System.env.gradlePublishKey)
        System.properties.setProperty('gradle.publish.secret', System.env.gradlePublishSecret)
    }
    tasks.publishPlugins.dependsOn(delegate)
}

publishing {
    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri("https://maven.pkg.github.com/ianpilipski/IPTech-GradleXcodePlugin")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
}

version = '0.1.2-SNAPSHOT'
group 'com.iptech.gradle'
description = 'A plugin that will build xcode projects'

pluginBundle {
    website = 'https://github.com/ianpilipski/IPTech-GradleXcodePlugin'
    vcsUrl = 'https://github.com/ianpilipski/IPTech-GradleXcodePlugin'
    tags = ['xcode', 'iptech']
}

gradlePlugin {
    plugins {
        xcodePlugin {
            id = 'com.iptech.gradle.xcode-plugin'
            displayName = 'IPTech Xcode Plugin'
            description = 'A plugin that will build xcode projects'
            implementationClass = 'com.iptech.gradle.xcode.XcodePlugin'
        }
    }
}

dependencies {
    implementation gradleApi()
    implementation 'com.googlecode.plist:dd-plist:1.23'
    testImplementation('org.codehaus.groovy:groovy-all:2.5.7')
    testImplementation('org.spockframework:spock-core:1.3-groovy-2.5')
    testImplementation('cglib:cglib-nodep:3.3.0')
    testImplementation gradleTestKit()
}
